plugins {
    id 'java'
    id 'jacoco'
}

group 'alcool70'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
    reports {
        junitXml.enabled = true
    }
    testLogging {
        events("passed", "skipped", "failed")
    }
    jacoco {
        enabled = true
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }

        rule {
            enabled = true
            element = 'CLASS'
            includes = ['exercicios.ex01.*']
            excludes = ['exercicios.ex01.ValoresITF']

            limit {
                counter = 'LINE'
                value = 'TOTALCOUNT'
                minimum = 1.0
            }
        }

        rule {
            enabled = true
            element = 'BUNDLE'

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
            limit {
                counter = 'COMPLEXITY'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}